apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "org.jetbrains.dokka"

dokka {
    outputFormat = 'markdown'
    outputDirectory = "$buildDir/javadoc"

    configuration {
        sourceLink {
            path = "src/main/java"
            url = "https://github.com/proba-ai/proba-sdk-android/tree/master/ProbaSdk/src/main/java/"
            lineSuffix = "#L"
        }
    }
}

task androidJavadocsJar(type: Jar, dependsOn: dokka) {
    archiveClassifier.set('javadoc')
    from dokka.outputDirectory
}

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
}
afterEvaluate{
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId group
                artifactId 'probasdk'
                version version

                artifact androidJavadocsJar
                artifact androidSourcesJar

                pom {
                    name = 'Proba Android SDK'
                    description = 'Proba mobile SDK for Android platform'
                    url = 'https://github.com/proba-ai/proba-sdk-android'
                    inceptionYear = '2021'
                    licenses {
                        license {
                            name = 'MIT License (MIT)'
                            url = 'https://github.com/proba-ai/proba-sdk-android/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            name = 'Proba Team'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/proba-ai/proba-sdk-android.git'
                        url = 'https://github.com/proba-ai/proba-sdk-android'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "sonatype"

                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}